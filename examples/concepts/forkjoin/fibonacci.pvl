// -*- tab-width:2 ; indent-tabs-mode:nil -*-
//:: cases FibonacciPVL
//:: suite puptol
//:: tools silicon
//:: verdict Pass
/*
 * This example verifies the permission used for 
 * a fork/join computation of a Fibonacci number.
 *
 * To verify this example, use:
 *
 *   vct --silver=silicon fibonacci.pvl
 */
// begin(all)

pure int fib(int n)=n<2?1:fib(n-1)+fib(n-2);

class Fib {
  int input, output;
  
  requires Perm(input,1\10) ** Perm(output,1);
  ensures  Perm(input,1\10) ** Perm(output,1) ** output==fib(input);
  run {
    if (input<2) {
      output = 1;
    } else {
      Fib f1 = new Fib(input-1);
      Fib f2 = new Fib(input-2);
      fork f1;  fork f2;
      join f1;  join f2;
      output = f1.output + f2.output;
    }
  }
  
  ensures Perm(input,1) ** Perm(output,1) ** input==n;
  ensures idle(this);
  constructor(int n){
    input = n;
  }
}
// end(all)

